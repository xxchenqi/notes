# 汇编

https://developer.android.com/ndk/guides/other_build_systems?hl=zh-cn



```
预处理
clang -target aarch64-linux-android21 -E hello.c -o hello.i
编译
clang -target aarch64-linux-android21 -S hello.i -o hello.s
汇编(如果源文件有多个，就会编译多个.o文件)
clang -target aarch64-linux-android21 -c hello.s -o hello.o
执行程序
clang -target aarch64-linux-android21 hello.o -o hello
链接
clang -target aarch64-linux-android21 hello.s -o hello


32位
预处理
clang -target arm-linux-android21 -E hello.c -o hello.i
编译
clang -target arm-linux-android21 -S hello.i -o hello.s
汇编
clang -target arm-linux-android21 -c hello.s -o hello.o
执行程序
clang -target arm-linux-android21 hello.o -o hello
链接
clang -target arm-linux-android21 hello.s -o hello


```



## ARM汇编语言的结构

```
处理器

段section
    .text
    .section	.rodata.str1.1,"aMS",%progbits,1
    .section	".note.GNU-stack","",%progbits

符号
    外部符号
        bl	printf

子程序
    .globl	main
    .type	main,%function
    .code	32
    
main:
    .fnstart
    .fnend

标号

注释 @
```

## 寄存器

```
R0~R7 未分组寄存器
R8~R12 是分组寄存器
R13~R14
    R13 SP
    R14 LR
R15 PC
CPSR
```

## 指令解释

### 寻址方式

```
立即数寻址
寄存器寻址
寄存器移位寻址
    LSL 逻辑左移
    LSR 逻辑右移
    ROR 循环右移 
    ASR 算术右移
    RRX 扩展的循环右移
   
Load Store的寻址方式:(ldr和str)
    立即数寻址 ldr r0, =0x2
    寄存器
    基址变址 ldr r0, [r3, r2, LSL #2]

栈寻址
    stmfd
    ldmfd
```

### 汇编指令

```
跳转指令
    B   强制跳转指令
    BL  带返回的跳转指令， LR
    BLX 带返回和带状态切换的跳转指令， arm -> thumb, thumb -> arm
    BX  带状态切换的跳转指令

数据处理指令
    mov, add, sub, and, eor, orr, bic

    mov r0, r1
    add r0, r1, r2  @ r0 = r1 + r2
    sub r0, r1, r2
    and r0, r1, r2  @ r0 = r1 & r2
    eor r0, r1, r2  @ r0 = r1 ^ r2
    orr r0, r1, r2  @ r0 = r0 | r2
    bic r0, r1, #0xF @0x12345678 -> 0x12345670
    
乘法指令
    MUL r0, r1, r2   @ r0 = r1 * r2
    MLA r0, r1, r2, r3  @ r0 = r1 * r2 + r3
    SMULL r0, r1, r2, r3  @ r0 = (r2 * r3)的低32位，r1 = (r2 * r3)的高32位，
    SMLAL r0, r1, r2, r3  @ r0 = (r2 * r3)的低32位 + r0，r1 = (r2 * r3)的高32位 + r1，
    UMULL r0, r1, r2, r3  @ r0 = (r2 * r3)的低32位，r1 = (r2 * r3)的高32位，
    UMLAL r0, r1, r2, r3  @ r0 = (r2 * r3)的低32位 + r0，r1 = (r2 * r3)的高32位 + r1，
    
    
内存访问指令
    ldr  4字节读取
    ldrb 1字节读取
    ldrh 2字节读取

    str  4字节写入
    strb 1字节写入
    strh 2字节写入
```



### 常用总结

```assembly
参数:r0~r3
返回值:r0
r0(Register 0):是一个通用寄存器,可以存储各种类型的数据,通常用于存储函数的第一个参数或返回值。
lr(Link Register):是链接寄存器，用于存储函数调用的返回地址。
详细解释:用于存储调用指令的下一条指令的地址，也就是函数返回后程序应该继续执行的地址。当函数调用子函数时，当前函数的返回地址会被保存到 lr 寄存器中，以便在子函数执行完毕后能够正确返回到调用者。


push {r11, lr}		@保存r11, lr到栈里面, lr记录函数的返回地址
pop	{r11, lr}		@恢复r11, lr

stmfd sp!, {r5-r11, lr}   @相当于push {r5,r6,r7,r8,r9,r10,r11,lr}
ldmfd sp!, {r5-r11, lr}	  @相当于pop {r5,r6,r7,r8,r9,r10,r11,lr}

mov	r11, sp				@r11 = sp
mov	r0, #12				@r0=12
mov r0, #0x12		    @r0=0x12(16进制的数字) 
mov r0, r1, LSL #4      @r0 = r1 << 4
mov r0, r0, LSR #2      @r0 = r0 >> 2

add	r0, pc, r0			@r0=pc+r0
sub	sp, sp, #24			@sp=sp-24

str	r2, [r11, #-4]		@r11-4=r2
str	r1, [sp, #12]		@sp+12=r1

strb r0, [r3, #4]    @r3+4=r0 (r0存储一个byte到r3的第四个字节) , strh就是存储2个byte

ldr r0, [r3, #4]	@r0=*(r3+4)
ldr r0, =0x2		@r0=0x2
ldr r0, [r3]		@r0=*r3
ldr r0, [r3, #4]!	@r0=*(r3 + 4), r3 += 4
ldr r0, [r3, r2, LSL #2] @r0 = *(r3 + (r2 << 2))


ldmia r3, {r4-r6}   @r4=r3,r5=r3+4,r6=r3+8

bl	printf				@子程序的调用， r0~r3传递参数， 大于4个参数的时候，多的参数用栈传参数

b .LABEL1               @强制跳转到.LABEL1
bx	lr					@bx lr， 跳转到lr地址， arm -> thumb 或者 thumb -> arm

```



### 指令详解

https://developer.arm.com/documentation/ddi0406/cd?lang=en

https://armconverter.com/?disasm

```
规范:
B<c> <label>

31 30 29 28 27 26 25 24 23 22 21 20 19 18 17 16 15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0
cond....... 1  0  1  0  imm24...

cond 0000 EQ Equal
1010 是固定的(每个指令都有固定位)

举例:
0000 1010 0000 0000 0000 0000 0000 0001
=
0xA000001
通过https://armconverter.com/?disasm转换(小端输入:01 00 00 0A)
-> beq #0xc


分析:
0000 1010 0000 0000 0000 0000 0000 0001

固定位 = 1010
cond = 0000
imm24 = 0000 0000 0000 0000 0000 0001

相关函数
imm32 = SignExtend(imm24:’00’, 32);

imm24:’00’ -> 0000 0000 0000 0000 0000 000100, 这里不足32bit,需要SignExtend扩展

imm32 -> 0000 0000 0000 0000 0000 0000 0000 0100 -> 4


为什么是0xc而不是0x4?
因为PC需要往下移2个指令。

为什么要往下移2个指令?
arm执行指令是流水线，流水线分为3步，取地址，译码，执行。

流水线处理器是一种常见的处理器设计，它可以同时执行多条指令的不同阶段。当一条指令被取出时，下一条指令已经在解码阶段，再下一条指令则在执行阶段。
因此，PC 移动两个指令的位置通常是为了优化流水线处理器的性能，并且保证在分支跳转时的正确执行。

例如:
在执行00: xxxx时，04: yyyy正在译码阶段,08: beq #0xc 在取地址阶段,此时PC取的地址是08
在执行04: yyyy时，08: beq #0xc正在译码阶段,此时PC取的地址是0C
在执行08: beq #0xc时,此时PC取的地址是10
所以-> 0x10-0x08 = 0x8， 0x8+4 = 0xC


00: beq #0xc
04:
08:
0C:

                PC 08           PC 0C          PC 10

00: xxxx        执行
04: yyyy        译码            执行
08: beq #0xc    取地址          译码            执行
0C:                            取地址          译码
10:                                           取地址
14:


```



### 代码转汇编

数据类型、运算符、分支与跳转、循环

```c
int main(int argc, char const *argv[])
{
    /*
    	mov	r0, #97
    	strb	r0, [r11, #-13] 
    	char占用1个字节,所以是strb
    */
    char ch1 = 'a'; 
    
    /*
    	mov	r0, #16
		strh	r0, [r11, #-16]
		short占用2个字节,所以是strh
    */
    short s1 = 0x10;
    
    /*
    	mov	r0, #256
		str	r0, [r11, #-24]
		int占用4个字节,所以是str
    */
    int n1 = 0x100;
        
    /*
		mov	r0, #17825792
		orr	r0, r0, #1073741824
		str	r0, [r11, #-32]
		
		解释:
		17825792 | 1073741824 = 1091567616 = 01000001000100000000000000000000
		0 10000010 00100000000000000000000
		
		根据IEEE754公式：(-1)^S * 1.M * 2^(E-127)
		S：sign 阶符 (1位) = 0
        E：exponent 阶码 (8位) = 10000010
        M：mantissa 尾数 (23位) = 00100000000000000000000
		E-127 = 130 - 127 = 3
		1.M = 1.001
		1.M * 2^3 = 1001 = 9
	*/
    float f1 = 9.0f;

    /*
        mov	r0, #3997696
        orr	r0, r0, #1073741824
        str	r0, [sp, #28]
        解释:
        3997696 | 1073741824 = 1077739520 = 01000000001111010000000000000000
        0 10000000011 11010000000000000000
        根据IEEE754公式：(-1)^S * 1.M * 2^(E-1023)
        S：sign 阶符 (1位) = 0
        E：exponent 阶码 (11位) = 10000000011
        M：mantissa 尾数 (52位) = 11010000000000000000...
        E-1023 = 1027 - 1023 = 4
        1.M = 1.1101
        1.M * 2^4 = 11101 = 29
    */
    double d1 = 29.0;
	
    /*
        ldrb	r0, [r11, #-13]
        ldrb	r1, [r11, #-14]
        add	r0, r0, r1
        strb	r0, [sp, #15]
    */
    char ch3 = ch1 + ch2;
    
    /*
        ldrh	r0, [r11, #-16]
        ldrh	r1, [r11, #-18]
        sub	r0, r0, r1
        strh	r0, [sp, #12]
    */
    short s3 = s1 - s2;
    
    /*
        ldr	r0, [r11, #-24]
        ldr	r1, [r11, #-28]
        mul	r2, r0, r1
        str	r2, [sp, #8]
    */
    int n3 = n1 * n2;
    
    /*
        mov	r0, #4096		@r0 = 0x1000
        str	r0, [sp, #8]
        ldr	r0, [sp, #8]
        ldr	r1, .LCPI0_1	@r1 = 0x92492493 (编译器生成的魔数)
        smull	r2, r3, r0, r1 
        add	r1, r3, r0
        asr	r3, r1, #2
		add	r1, r3, r1, lsr #31
.LCPI0_1:
		.long	2454267027              @ 0x92492493
		
		解释:
		smull	r2, r3, r0, r1 
		r0*r1 = 0x1000 * 0x92492493 = 0x92492493000
		低位放在r2，高位放在r3
		92492493 高位是1,所以用1来填充,1111B = 0xF
		r3 = fffff924
		r2 = 92493000
		
		add	r1, r3, r0
		r1 = r3 + r0 = 0xfffff924 + 0x1000 = 0x100000924 -> add不带符号,不保存高位 0x924
		
		asr	r3, r1, #2
		r3 = r1 >> 2 = 0x924 >> 2 = 0x249
		
		add	r1, r3, r1, lsr #31
		r1 = r3 + (r1 >> 31) = 0x249 + (0x924>>31) = 0x249
    */
    n3 = 0x1000;
    int n4 = n3 / 7;

	/*
		cmp	r0, #5
		bgt	.LBB0_2		@ r0 > 5，跳转到 .LBB0_2 标签处执行。
		b	.LBB0_1		@ 无条件分支指令
	*/
    if (n5 < 6) {
        printf("if n5:%d\r\n",n5);
    } else {
    	printf("else n5:%d\r\n",n5);
    }
	
    /*
    case比较少就用if else,case比较多就是查表的方式
    
.LBB0_3:
    	ldr	r0, [sp, #12]
        sub	r0, r0, #1
        cmp	r0, #3
        str	r0, [sp, #8]    
        bhi	.LBB0_10	@无符号大于
        
        adr	r0, .LJTI0_0
        ldr	r1, [sp, #8]            @ 4-byte Reload
        ldr	r2, [r0, r1, lsl #2]
        add	pc, r0, r2

        .p2align	2
.LJTI0_0:
        .long	.LBB0_6-.LJTI0_0
        .long	.LBB0_7-.LJTI0_0
        .long	.LBB0_8-.LJTI0_0
        .long	.LBB0_9-.LJTI0_0


    */
    switch (n5)
    {
    case 1:
        printf("case 1\r\n");
        break;
    case 2:
        printf("case 2\r\n");
        break;
    case 3:
        printf("case 3\r\n");
        break;
    case 4:
        printf("case 4\r\n");
        break;
    
    default:
        printf("case default\r\n");
        break;
    }
    
    //b .LBB0_1
    //goto goto_label;

goto_label:
    printf("goto");

    /*
    .LBB0_1:                                @ =>This Inner Loop Header: Depth=1
        ldr	r0, [sp, #16]
        cmp	r0, #19
        bgt	.LBB0_4		 @ r0 > 19，跳转到 .LBB0_4 标签处执行。
        b	.LBB0_2
    .LBB0_2:                                @   in Loop: Header=BB0_1 Depth=1
        ldr	r1, [sp, #16]
        ldr	r0, .LCPI0_0
    .LPC0_0:
        add	r0, pc, r0
        bl	printf
        b	.LBB0_3
    .LBB0_3:                                @   in Loop: Header=BB0_1 Depth=1
        ldr	r0, [sp, #16]
        add	r0, r0, #1		@ 相当于i++
        str	r0, [sp, #16]
        b	.LBB0_1
    */
	for (int i = 0; i < 20; i++)
    {
        printf("for i : %d\r\n", i);
    }
    
    /*
    .LBB0_4:
        mov	r0, #30
        str	r0, [sp, #12]
        b	.LBB0_5
    .LBB0_5:                                @ =>This Inner Loop Header: Depth=1
        ldr	r0, [sp, #12]
        cmp	r0, #0
        beq	.LBB0_7
        b	.LBB0_6
    .LBB0_6:                                @   in Loop: Header=BB0_5 Depth=1
        ldr	r1, [sp, #12]
        ldr	r0, .LCPI0_1
    .LPC0_1:
        add	r0, pc, r0
        bl	printf
        ldr	r1, [sp, #12]
        sub	r1, r1, #1
        str	r1, [sp, #12]
        b	.LBB0_5
    */
    int j = 30;
    while (j)
    {
        printf("while j : %d\r\n", j);
        j--;
    }
    
    /*
    .LBB0_7:
        mov	r0, #40
        str	r0, [sp, #8]
        b	.LBB0_8
    .LBB0_8:                                @ =>This Inner Loop Header: Depth=1
        ldr	r0, [sp, #8]
        sub	r1, r0, #1
        str	r1, [sp, #8]
        ldr	r1, .LCPI0_2
    .LPC0_2:
        add	r1, pc, r1
        str	r0, [sp, #4]            @ 4-byte Spill
        mov	r0, r1
        ldr	r1, [sp, #4]            @ 4-byte Reload
        bl	printf
        b	.LBB0_9
    .LBB0_9:                                @   in Loop: Header=BB0_8 Depth=1
        ldr	r0, [sp, #8]
        cmp	r0, #0
        bne	.LBB0_8
    */
    int k = 40;
    do
    {
       printf("do while k : %d\r\n", k--);

    } while (k);


    return 0;
}
```



函数、结构体、数组、位操作

```



```







## 架构差异

### 16-bit thumb 指令集

TODO

### 32-bit thumb 指令集

TODO

### aarch64 arm的区别

```
寄存器:
x0 ~ x30

x0 ~ x7 传递参数,x0为返回结果
x8 结果位置寄存器(返回值大于x0寄存器长度，将结果取地址放在x8)
x9 ~ x15 临时寄存器
x16 ~ x17 内部库存调用寄存器
x18 临时寄存器
x19~x28 备份
x29 FP 栈指针寄存器
x30 LR
SP
PC
CPSR
```



## 例子

```assembly
	.text
	.syntax unified
	.cpu	arm7tdmi
	.globl	main                    @ -- Begin function main
	.type	main,%function
	.code	32                      @ @main
main:
	.fnstart
	push	{r11, lr}		@保存寄存器lr, r11到栈里面, lr记录函数的返回地址
	mov	r11, sp				@把sp赋值给r11  寄存器寻址
	sub	sp, sp, #24			@sp=sp-24	@sub rm, rn, #24 rm=rn-24
	mov	r2, #0				@r2=0			立即数寻址
	str	r2, [r11, #-4]		@把r2的值存到r11-4的地址上		基址寻址
	str	r0, [r11, #-8]		@把r0的值存到r11-8的地址上		基址寻址
	str	r1, [sp, #12]		@把r1的值存到sp+12的地址上		基址寻址
	ldr	r0, .LCPI0_0		@ldr    r0,  [pc,  #40], 从pc+40读取内容存到r0, PC是当前指令的地址+两条指令长度
.LPC0_0:
	add	r0, pc, r0			@r0=pc+r0
	ldr	r1, .LCPI0_1
.LPC0_1:
	add	r1, pc, r1
	str	r2, [sp, #8]            @ 4-byte Spill
	bl	printf				@子程序的调用， r0~r3传递参数， 大于4个参数的时候，多的参数用栈传参数， si单步步入， 
							@r0用作返回值
	ldr	r1, [sp, #8]            @ 4-byte Reload
	str	r0, [sp, #4]            @ 4-byte Spill
	mov	r0, r1				@r0返回值
	mov	sp, r11				@恢复栈
	pop	{r11, lr}			@恢复r11, lr
	bx	lr					@bx lr， 跳转到lr地址， arm -> thumb 或者 thumb -> arm
@ %bb.1:
.LCPI0_0:
	.long	.L.str-(.LPC0_0+8)
.LCPI0_1:
	.long	.L.str.1-(.LPC0_1+8)
.Lfunc_end0:
	.size	main, .Lfunc_end0-main
	.fnend
                                        @ -- End function
	.type	.L.str,%object          @ @.str
	.section	.rodata.str1.1,"aMS",%progbits,1
.L.str:
	.asciz	"hello %s!\r\n"
	.size	.L.str, 12

	.type	.L.str.1,%object        @ @.str.1
.L.str.1:
	.asciz	"arm"
	.size	.L.str.1, 7

	.section	".note.GNU-stack","",%progbits

```



```assembly
	.text
	.syntax unified
	.cpu	arm7tdmi
	.globl	main
	.type	main,%function
	.code	32
main:
	@push {r4, r5, r6, r7, r8, lr}
	stmfd sp!, {r5-r11, lr}

	mov r3, r1
	
	b .LABEL1

	mov r1, #1			@ r1 = 1
	mov r2, #2			@ r2 = 2

	mov r0, #0   		@ #，立即数寻址
	mov r0, #123 		@ 123 10进制的数字
	mov r0, #0x12		@ 0x12 16进制的数字, r0=0x12
	mov r0, #12

.LABEL1:
	mov r0, r1			@ r0=r1
	add r0, r1, r2		@ r0 = r1 + r2
	
	mov r0, r1, LSL #4  @ r0 = r1 << 4
	
	mov r0, r0, LSR #2  @ r0 = r0 >> 2

	ldr r0, =0x2		@ =0x2

	ldr r0, [r3]		@ r0 = *r3
	
	ldr r0, [r3, #4]		@ r0 = *(r3+4)
	ldr r0, [r3, #8]		@ r0 = *(r3+8)

	@ldr r0, [r3, r2, LSL #2] @r0 = *(r3 + (r2 << 2))

	mov r0, #2
	str r0, [r3, #4]

	@ldr r0, [r3, #4]!		@ r0 = *(r3 + 4), r3 += 4
	mov r0, #4
	strb r0, [r3, #4]
	add r0, #1
	strb r0, [r3, #5]
	
	ldmia r3, {r4-r6}

	ldmfd sp!, {r5-r11, lr}
	@pop {r4, r5, r6, r7, r8, lr}

	bx lr


.LABEL:
	.long 0x10
```



### printf例子

```assembly
	.text
	.syntax unified
	.cpu	arm7tdmi
	.globl	main
	.type	main,%function
	.code	32

main:
    push {lr}
    @参数 r0, r1, r2, r3
    @ldr r0, [r1] 第一个参数(执行路径)赋值给r0
    @ldr r0, [r1, #4] 第二个参数(执行路径后面的参数)赋值给r0
    ldr r0, [r1, #8] @第三个参数赋值给r0
    bl printf @打印r0

    mov r0, #0
    pop {lr}
    bx lr @ 带返回的跳转指令， LR

@main(int argc, char* argv[])
```

### ls例子

```assembly
	.text
	.syntax unified
	.cpu	arm7tdmi
	.globl	main
	.type	main,%function
	.code	32
main:
    push {r4, r5, lr}
    
    @arm_ls path

    ldr r0, [r1, #4]
    bl opendir
    cmp r0, #0
    beq .LABEL_EXIT
    mov r4, r0
    bl readdir
    cmp r0, #0
    @ 如果两个操作数相等，则跳转到目标地址执行指令。
    beq .LABEL_CLOSE_DIR
	
	@.format_str 相对于 .LABLE0 的偏移量。
    ldr r5, .format_str_
.LABLE0:
	@PC存储了当前执行指令的地址，在加上.format_str 相对于 .LABLE0 的偏移量,就能获取到"%s\r\n"字符串
    add r5, pc, r5

.LOOP_READDIR:
    @printf("%s\r\n", str) , 这里存储到r1，因为是第二个参数
    @这里加0x13偏移是因为数据就在0x13偏移的位置上
    add r1, r0, #0x13   
    @ "%s\r\n" 给到 r0，作为第一个参数
    mov r0, r5
    bl printf
    mov r0, r4
    bl readdir
    cmp r0, #0
    @如果 r0 的值不为 0，则跳转到 .LOOP_READDIR 标签处
    bne .LOOP_READDIR

.LABEL_CLOSE_DIR:
    mov r0, r4
    bl closedir

.LABEL_EXIT:
    mov r0, #0
    pop {r4, r5, lr}
    bx lr


.format_str_:
	@加上 8 得到的结果是 .LABLE0 后面指令的地址。
	@.format_str 的地址中减去 .LABLE0 后面指令的地址，这样就得到了 .format_str 相对于 .LABLE0 的偏移量。
    .long .format_str-(.LABLE0 + 8)

.format_str:
	@asciz定义字符串，以0结尾
	@ascii定义字符串，没有0结尾
    .asciz "%s\r\n"
```





## GDB

使用

```
# 路径 ndk\21.1.6352462\prebuilt\android-arm\gdbserver
./gdbserver :11678 ./hello

adb forward tcp:11678 tcp:11678

https://github.com/hugsy/gef
linux可能自带的有gdb这个工具，但是它不能用来调试arm程序，需要安装gdb-multiarch
gef:target remote localhost:11678
```

区域介绍

```
寄存区registers、栈stack、汇编code、线程threads、调用栈trace
```

常用指令

```
给main函数下断点
b main

查看main函数地址
disassemble main

给地址下断点
b *0x004006d0

查看断点信息
info b

继续运行
c

汇编单步步过
ni

源码单步步过
n

单步步入
si

跳出函数
finish

十六进制打印
p/x *0x004006d0


gef的打印命令
hexdump

展示架构
show architecture

```





## 其他

arm手册

https://developer.arm.com/documentation/ddi0406/cd?lang=en
